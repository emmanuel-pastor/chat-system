plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id "org.beryx.jlink" version "2.24.1"
}

group 'com.simple-smart-apps'
version '1.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.simplesmartapps.chatsystem'
    mainClass = 'com.simplesmartapps.chatsystem.ChatSystemApplication'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Chat System'
        imageName = "chatsystem"
        mainClass = "com.simplesmartapps.chatsystem.ChatSystemApplication"
    }
    jpackage {
        imageName = "chatsystem"
        mainClass = "com.simplesmartapps.chatsystem.ChatSystemApplication"
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']
        }
    }
}

run {}

javafx {
    version = '17.0.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('org.kordamp.ikonli:ikonli-javafx:12.2.0')
    implementation 'org.kordamp.ikonli:ikonli-fontawesome-pack:12.2.0'
    implementation "com.google.inject:guice:5.0.1"
    implementation('org.json:json:20210307')
    implementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    implementation "org.xerial:sqlite-jdbc:3.36.0.3"

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}